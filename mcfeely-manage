#!/usr/bin/perl
# vi:ts=4:sw=4:sm:ai:et:

# mcfeely        Asynchronous remote task execution.
# Copyright (C) 1999 Kiva Networking
#
# This program is free software; you can redistribute it and/or
# modify it under the terms of the GNU General Public License
# as published by the Free Software Foundation; either version 2
# of the License, or (at your option) any later version.
#
# This program is distributed in the hope that it will be useful,
# but WITHOUT ANY WARRANTY; without even the implied warranty of
# MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
# GNU General Public License for more details.
#
# You should have received a copy of the GNU General Public License
# along with this program; if not, write to the Free Software
# Foundation, Inc., 59 Temple Place - Suite 330, Boston, MA  02111-1307, USA.
#
# You may contact the maintainer at <mcfeely-maintainer@systhug.com>.

use strict;
use McFeely::Internal;
use McFeely::Log;
use McFeely::Const;
use Fcntl;
use IO::File;
use IO::Handle;
use IO::Select;
use IO::Dir;
use POSIX qw(:sys_wait_h);


$SIG{CHLD} = \&reaper;

use vars qw( $Asked_to_exit $Tasks_in_progress @Tasks %Task %Job
             $rout $rin $srr $siw $select $Srr_readbuffer);

$Srr_readbuffer = '';

$select = new IO::Select;

my $pid = shift;
my $in  = shift;
my $out = shift;

$srr = new IO::Handle;
$siw = new IO::Handle;

# our pipes to mcfeely-spawn
$srr->fdopen($in, "r")  or die "open SRR (fd $in) failed: $!\n";
$siw->fdopen($out, "w") or die "open SIW (fd $out) failed: $!\n";

$select->add($srr);

$siw->autoflush(1);
STDOUT->autoflush(1);

require 'chdir.pl';
require 'safe_to_exit.pl';
require 'files.pl';
require 'tasks.pl';
require 'jobs.pl';
require 'attempt_tasks.pl';
require 'read_results.pl';

chdir_top() or bail("Could not chdir: $!; exiting.");
chdir 'queue' or bail("Could not chdir: $!; exiting.");

plog("starting");

scan_job('job', 0);

until ($Asked_to_exit && safe_to_exit() ) {
    scan_job('newj', 1);
    attempt_tasks();
    do_select();
    read_results();
}

plog("exiting");

# we've lost the spawner, that's not supposed to 
# happen
# XXX: if this happens mcfeely is going to quit and supervise
# XXX: we'll start it back up again. we don't want that. 
# XXX: perhaps just run without supervise? or supervise -o
sub reaper  {
    my $kid;
    $kid = waitpid($pid, WNOHANG);
    if ($kid == $pid) {
        plog("mcfeely-spawn has died for unknown reasons");
        plog("queue status unreliable");
        plog("Tasks in progress: $Tasks_in_progress");
        $Tasks_in_progress = 0;
        $Asked_to_exit = 1;
    }
}


# wait on the trigger and other filehandles
sub do_select() {
    # $select is global in mcfeely-manage's name space

    my $trigger = new IO::File;
    my @hits;
    my $buf;

    # open the trigger without blocking
    $trigger->open('trigger', O_RDONLY|O_NONBLOCK)
        or plog "Cannot open trigger: $!";

    # wait for activity
    $select->add($trigger);
    @hits = $select->can_read(SLEEPYTIME()) ;

    # read from the trigger so that the select knows we are 
    # paying attention
    foreach (@hits) {
        if ($_ == $trigger) {
            $trigger->read($buf, 1);
        }
    }

    $select->remove($trigger);
    close($trigger);
}

__END__

=head1 NAME

mcfeely-manage - queue management system for McFeely

=head1 SYNOPSIS

B<mcfeely-manage> C<E<lt>mcfeely-spawn pidE<gt>> C<E<lt>fd of in
pipeE<gt>> C<E<lt>fd of out pipeE<gt>>

=head1 DESCRIPTION

The B<mcfeely-manage> program is responsible for reading and
managing the McFeely queue. mcfeely-manage listens on a FIFO
called C<trigger> in the queue directory. It learns about new
jobs and their associated tasks and if a task needs to be done
the task number is passed to B<mcfeely-spawn> down the out pipe.
B<mcfeely-manage> then waits for results information on the in
pipe. Results are stored in the queue. When all tasks in job have
succeeded or failed, that job is done. B<mcfeely-manage> operates
on multiple jobs and tasks concurrently.

See the OVERVIEW document a detailed description of how tasks
move through mcfeely-manage.

=head1 SEE ALSO

L<mcfeely-start>,
L<mcfeely-spawn>,
B<OVERVIEW>

=head1 COPYRIGHT

This program is copyright 2000 Kiva Networking. It is free software;
you can redistribute it and/or modify it under the terms of the GNU
General Public License as published by the Free Software Foundation;
either version 2 of the License, or (at your option) any later version.
Please see the file COPYING in the distribution.

=head1 AUTHORS

Matt Liggett E<lt>mml@pobox.comE<gt>, Chris Dent E<lt>cdent@systhug.comE<gt>,
Adrian Hosey E<lt>ahosey@systhug.comE<gt>

=cut
